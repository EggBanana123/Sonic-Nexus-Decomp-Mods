//--------------Sonic Nexus SSZ Parachute-------------//
//----Scripted by Christian Whitehead "The Taxman"----//

//----Player Directions----//
#alias 1 : FACING_LEFT
#alias 0 : FACING_RIGHT

//----Player Movement States----//
#alias 1 : AIR_MOVEMENT
#alias 8 : GETTING_HURT

#alias 10 : ANI_JUMPING

#alias 34 : TYPE_PARACHUTE
#alias Object.Value0 : Object.PlayerNo
#alias Object.Value1 : Object.StartX
#alias Object.Value2 : Object.StartY
#alias Object.Value3 : Object.XVelocity
#alias Object.Value4 : Object.YVelocity

sub ObjectMain

	switch Object.State
	case 1
		Player.EntityNo=Object.Value0
		Player.Animation=43

		if Player.Left==true
			Player.Direction=FACING_LEFT
		end if
		if Player.Right==true
			Player.Direction=FACING_RIGHT
		end if

		if Player.Direction==FACING_RIGHT
			if Player.Speed<-196608
				Player.Speed*=104
				Player.Speed>>=7
			end if
			if Player.Speed>196608
				Player.Speed*=104
				Player.Speed>>=7
				if Player.Speed<196608
					Player.Speed=196608
				end if
			else
				Player.Speed+=4096
				if Player.Speed>196608
					Player.Speed=196608
				end if
			end if
		else
			if Player.Speed>196608
				Player.Speed*=104
				Player.Speed>>=7
			end if
			if Player.Speed<-196608
				Player.Speed*=104
				Player.Speed>>=7
				if Player.Speed>-196608
					Player.Speed=-196608
				end if
			else
				Player.Speed-=4096
				if Player.Speed<-196608
					Player.Speed=-196608
				end if
			end if
		end if

		if Player.YVelocity<65536
			Player.YVelocity+=8192
		end if

		Player.XVelocity=Player.Speed
		Object.XPos=Player.XPos
		Object.YPos=Player.YPos
		TempValue0=Player.CollisionTop
		TempValue0-=22
		TempValue0<<=16
		Object.YPos+=TempValue0

		if Player.State==GETTING_HURT
			Object.Priority=1
			Object.State=2
			ArrayPos0=Object.Value0
			Object[ArrayPos0].PropertyValue=0
			Object.XVelocity=Player.XVelocity
		end if

		if Player.Gravity==false
			Object.Priority=1
			Object.State=2
			ArrayPos0=Object.Value0
			Object[ArrayPos0].PropertyValue=0
			Object.XVelocity=Player.XVelocity
		else
			Object.YVelocity=Player.YVelocity
		end if

		if KeyPress.ButtonA==true
			PlaySfx(0,false)
			Player.State=AIR_MOVEMENT
			Player.YVelocity=-262144
			Player.Gravity=true

			Object.Priority=1
			Object.State=2
			ArrayPos0=Object.Value0
			Object[ArrayPos0].PropertyValue=0
			Object.XVelocity=Player.XVelocity
        else
            if KeyPress.ButtonB==true
			    PlaySfx(0,false)
			    Player.State=AIR_MOVEMENT
			    Player.YVelocity=-262144
			    Player.Gravity=true

			    Object.Priority=1
			    Object.State=2
			    ArrayPos0=Object.Value0
			    Object[ArrayPos0].PropertyValue=0
			    Object.XVelocity=Player.XVelocity
            end if
        end if
		break
	case 2
		if Object.YVelocity<65536
			Object.YVelocity+=8192
		end if

		Object.XPos+=Object.XVelocity
		Object.YPos+=Object.YVelocity

		if Object.OutOfBounds==true
			Object.State=3
			Object.XPos=Object.StartX
			Object.YPos=Object.StartY
		end if
		break

	case 3
		if Object.OutOfBounds==true
			Object.State=0
			Object.Priority=0
		end if
		break
	end switch

end sub

sub ObjectPlayerInteraction

	if Object.State==0
		PlayerObjectCollision(C_TOUCH,-16,20,16,24)
		if CheckResult==1
			Object.StartX=Object.XPos
			Object.StartY=Object.YPos

			Object.Value0=Player.EntityNo
			ArrayPos0=Player.EntityNo
			Object[ArrayPos0].PropertyValue=1
			Player.Animation=43
			Object.XPos=Player.XPos
			Object.YPos=Player.YPos
			TempValue0=Player.CollisionTop
			TempValue0-=22
			TempValue0<<=16
			Player.Speed=Player.XVelocity
			Object.YPos+=TempValue0
			Object.State=1
			Player.YVelocity=0
			Player.Gravity=true
			PlaySfx(16,false)
		end if
	end if

end sub

sub ObjectDraw

	switch Object.State
	case 0
		DrawSprite(0)
		break
	case 1
	case 2
		TempValue0=Object.Frame
		TempValue0>>=3
		DrawSprite(TempValue0)
		Object.Frame++
		if Object.Frame>63
			Object.Frame=0
		end if
		DrawSprite(TempValue0)
		break
	end switch

end sub

sub ObjectStartup

	LoadSpriteSheet("SSZ/Gimmicks.gif")

	//Parachute Frames
	SpriteFrame(-24,-20,48,43,50,62)
	SpriteFrame(-19,-20,48,43,1,106)
	SpriteFrame(-15,-20,48,43,50,106)
	SpriteFrame(-19,-20,48,43,1,106)
	SpriteFrame(-24,-20,48,43,50,62)
	SpriteFrame(-29,-20,48,43,1,55)
	SpriteFrame(-34,-20,47,43,72,18)
	SpriteFrame(-29,-20,48,43,1,55)

end sub

sub RSDK
	LoadSpriteSheet("SSZ/Gimmicks.gif")
	SetEditorIcon(Icon0,SingleIcon,-24,-20,48,43,50,62)
end sub
